# clear ptm and activity log data (USA/JPN/EUR/KOR only)
# remove all titles and data logged by the ptm module and activity log
# last update: 02/25/2018
# https://github.com/zweilous/gm9scripts/

if chk "EUR" $[REGION]
    set ACTLOG 00020222
elif chk "JPN" $[REGION]
    set ACTLOG 00020202
elif chk "KOR" $[REGION]
    set ACTLOG 00020272
elif chk "USA" $[REGION]
    set ACTLOG 00020212
else
echo "This script is only valid for\nUSA/JPN/EUR/KOR region 3DS systems."
goto abort
end

# activity log and ptm backup
if not ask "Create a PTM and Activity Log\nsavedata backup in\n$[GM9OUT]?"
	echo "PTM and Activity Log\nbackup not created."
	goto abort
end

findnot $[GM9OUT]/$[DATESTAMP]_$[SERIAL]_00010022_??? OUTPATH
cp 1:/data/$[SYSID0]/sysdata/00010022/00000000 $[OUTPATH]
echo "PTM savedata backup created succesfully:\n$[OUTPATH]"
findnot $[GM9OUT]/$[DATESTAMP]_$[SERIAL]_$[ACTLOG]_??? OUTPATH
cp 1:/data/$[SYSID0]/sysdata/$[ACTLOG]/00000000 $[OUTPATH]
echo "Activity Log backup created succesfully:\n$[OUTPATH]"

# delete ptm and activity log data
if not ask "This will clear all data from the\nPTM module and Activity Log.\nContinue?"
	echo "PTM and Activity Log reset failed."
	goto abort
end

allow -a 1:/
rm -o -s 1:/data/$[SYSID0]/sysdata/00010022/00000000
rm -o -s 1:/data/$[SYSID0]/sysdata/$[ACTLOG]/00000000
echo "PTM and Activity Log reset successful."

# abort
@abort