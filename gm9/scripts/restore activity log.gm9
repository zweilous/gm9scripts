# restore ptm and activity log data (any region)
# remove all titles and data logged by the ptm module and activity log
# last update: 02/25/2018
# https://github.com/zweilous/gm9scripts/

@start
labelsel -o -s "Select a restore option." re_*
goto cancel

@cancel
if ask "Cancel this operation?"
	goto abort
else
	goto start
end

# restore ptm data
@re_restore_ptm_data
if not filesel "Select a PTM savedata backup." $[GM9OUT]/??????_$[SERIAL]_00010022_??? RESTORE
	goto start
end
if not ask "Restore this PTM backup?\n$[RESTORE]"
	goto start
end
if not allow -a 1:/
	goto start
end

cp -w -n $[RESTORE] 1:/data/$[SYSID0]/sysdata/00010022/00000000
echo "PTM backup restored successfully."
goto start

# restore activity log data
@re_restore_activity_log_data
if chk "CHN" $[REGION]
	set ACTLOG 00020262
elif chk "EUR" $[REGION]
	set ACTLOG 00020222
elif chk "JPN" $[REGION]
	set ACTLOG 00020202
elif chk "KOR" $[REGION]
	set ACTLOG 00020272
elif chk "USA" $[REGION]
	set ACTLOG 00020212
elif chk "TWN" $[REGION]
	set ACTLOG 00020282
end
if not filesel "Select an Activity Log savedata backup." $[GM9OUT]/??????_$[SERIAL]_$[ACTLOG]_??? RESTORE
	goto start
end
if not ask "Restore this Activity Log backup?\n$[RESTORE]"
	goto start
end
if not allow -a 1:/
	goto start
end

cp -w -n $[RESTORE] 1:/data/$[SYSID0]/sysdata/$[ACTLOG]/00000000
echo "Activity Log backup restored successfully."
goto start

# abort
@abort
