# extract pokemon game .code
# extract a copy of .code from the specified pokemon game as a code.bin file
# last update: 02/24/2018
# https://github.com/zweilous/gm9scripts/

@start
labelsel -o -s "Choose a Pokemon game to extract .code from." game_*
goto cancel

@game_Pokemon_X
	set GAME "Pokemon X"
	set TIDLOW 00055D00
	goto title
@game_Pokemon_Y
	set GAME "Pokemon Y"
 	set TIDLOW 00055E00
 	goto title
@game_Pokemon_Omega_Ruby
	set GAME "Pokemon Omega Ruby"
 	set TIDLOW 0011C400
 	goto title
@game_Pokemon_Alpha_Sapphire
	set GAME "Pokemon Alpha Sapphire"
 	set TIDLOW 0011C500
 	goto title
@game_Pokemon_Sun
	set GAME "Pokemon Sun"
	set TIDLOW 00164800
	goto title
@game_Pokemon_Moon
	set GAME "Pokemon Moon"
	set TIDLOW 00175E00
	goto title
@game_Pokemon_Ultra_Sun
	set GAME "Pokemon Ultra Sun"
	set TIDLOW 001B5000
	goto title
@game_Pokemon_Ultra_Moon
	set GAME "Pokemon Ultra Moon"
	set TIDLOW 001B5100
	goto title

@title
labelsel -o -s "$[GAME] selected.\nExtract from:" title_*
goto cancel

@title_main_title
	set TIDHIGH 00040000
	goto type
@title_update_title
	set TIDHIGH 0004000E
	goto type
	
@type
labelsel -o -s "Select item to extract:" type_*
goto cancel

@type_.code
	goto process1
@type_romfs
	goto process2

@cancel
if ask "Cancel this operation?"
	goto abort
else
	goto start
end

# .code
@process1
if not filesel "Select a file to extract .code from." A:/title/$[TIDHIGH]/$[TIDLOW]/content/0000000?.app MOUNT
	goto start
end
imgmount $[MOUNT]
cp G:/exefs/.code $[GM9OUT]/$[TIDHIGH]$[TIDLOW]_code.bin
echo "$[GAME] .code extracted to:\n$[GM9OUT]/$[TIDHIGH]$[TIDLOW]_code.bin"
goto abort

# romfs
@process2
if not filesel "Select a file to extract romfs from." A:/title/$[TIDHIGH]/$[TIDLOW]/content/0000000?.app MOUNT
	goto start
end
imgmount $[MOUNT]
cp G:/romfs $[GM9OUT]/$[TIDHIGH]$[TIDLOW]_romfs
echo "$[GAME] romfs extracted to:\n$[GM9OUT]/$[TIDHIGH]$[TIDLOW]_romfs"
goto abort

# abort
@abort
imgumount